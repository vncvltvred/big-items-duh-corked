plugins {
	id 'fabric-loom' version '0.12-SNAPSHOT'
	id 'maven-publish'
	id 'com.matthewprenger.cursegradle' version '1.4.0'
	id 'com.modrinth.minotaur' version '2.1.1'
	id 'com.github.breadmoirai.github-release' version '2.2.12'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = "${project.mod_version}+mc${project.minecraft_version}"
group = project.maven_group

loom {}

repositories {
	maven { url "https://maven.siphalor.de" }
	maven { url "https://maven.shedaniel.me" }
	maven { url "https://jitpack.io" }
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/releases"
	}
	exclusiveContent {
		forRepository {
			maven {
				url "https://cursemaven.com"
			}
		}
		filter {
			includeGroup "curse.maven"
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation(include("de.siphalor:amecsapi-${minecraft_major_version}:${amecsapi_version}")) {
		exclude module: "nmuk-1.19"
	}

	// Required
	modApi("me.shedaniel.cloth:cloth-config-fabric:${clothconfig_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}
	modApi("com.terraformersmc:modmenu:${modmenu_version}")

//	modImplementation(include(fabricApi.module("fabric-key-binding-api-v1", project.fabric_api_version)))
//	modImplementation(include(fabricApi.module("fabric-registry-sync-v0", project.fabric_api_version)))
//	modRuntimeOnly("net.fabricmc.fabric-api:fabric-api:${fabric_api_version}")
	modImplementation("net.fabricmc.fabric-api:fabric-api:${fabric_api_version}")

	// EMi Compat
	modCompileOnly "dev.emi:emi-fabric:${emi_version}"
	modLocalRuntime "dev.emi:emi-fabric:${emi_version}"

	// EMIffect Compat
	modCompileOnly "curse.maven:emiffect-735528:${emiffect_fileid}"

	// EMI Loot Compat
	modCompileOnly "curse.maven:emi_loot-681783:${emi_loot_fileid}"

	// EMI Trades Compat
	modCompileOnly "curse.maven:emi-trades-villager-trading-emi-plugin-735523:${emi_trades_fileid}"

	// Tom's Simple Storage Mod Compat
	modCompileOnly "curse.maven:toms_storage-396826:${toms_storage_fileid}"

	// Bumblezone Compat
	modCompileOnly "curse.maven:bumblezone-363949:${bumblezone_fileid}"

	// REi Compat
	modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:${rei_version}"
	modRuntimeOnly("me.shedaniel:RoughlyEnoughItems-fabric:${rei_version}") {
		exclude group: "net.fabricmc.fabric-api"
	}
}

configurations.include.transitive = true
configurations.include.dependencies.each {
	if (!it.name.contains("bom")) {
		it.transitive = false
	}
}

processResources {
	inputs.property "version", version

	afterEvaluate {
		from(sourceSets.main.resources.srcDirs) {
			include "fabric.mod.json"
			expand "version": version
			duplicatesStrategy DuplicatesStrategy.INCLUDE
		}
	}
}

tasks.withType(JavaCompile).configureEach {
	it.getOptions().setEncoding("UTF-8")
}

tasks.register('sourcesJar', Jar) {
	dependsOn classes
	archiveClassifier.set("sources")
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}